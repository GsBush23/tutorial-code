\import Equality

-- 1. Define congruence for functions with two arguments via transport.
--    It is allowed to use any functions defined via transport.

\func pmap2 {A B C : \Type} (f : A -> B -> C) {a a' : A} (p : a = a') {b b' : B} (q : b = b')
  : f a b = f a' b'
  => transport (f a b = f a' __) q (transport (f a b = f __ b) p idp)

-- 2. Prove that 'transport' can be defined via 'pmap' and 'repl' and vice versa.
-- The function 'repl' says that if two types are equal then there exists a function between them.

-- Define 'repl' via 'transport'.
\func repl {A B : \Type} (p : A = B) (a : A) : B
  => transport (\lam x => x) p a

-- Define 'transport' via 'repl' and 'pmap'.
\func transport' {A : \Type} (B : A -> \Type) {a a' : A} (p : a = a') (b : B a) : B a'
  => repl (pmap B p) b

-- 3. Prove that left = right without using 'transport' or 'coe'.

\func left=right : left = right
  => path (\lam i => i)

-- 4. Prove that a = a' and b = b' implies (a,b) = (a',b') without using 'transport'.

\func pairEq {A B : \Type} {a a' : A} {b b' : B} (p : a = a') (q : b = b') : (a,b) = (a',b')
  => path (\lam i => (p @ i, q @ i))

-- 5. Prove that p = p' implies p.1 = p'.1 without using 'transport'.

\func projEq {A : \Type} (B : A -> \Type) {p p' : \Sigma (x : A) (B x)} (t : p = p') : p.1 = p'.1
  => path (\lam i => (t @ i).1)

-- 6. Prove that (\lam x => not (not x)) = (\lam x => x).

\func not (b : Bool) : Bool
  | true => false
  | false => true

\func notNotId : (\lam x => not (not x)) = (\lam x => x)
  => funExt (\lam _ => Bool) notNotId'
  \where
    \func notNotId' (x : Bool) : not (not x) = x
      | false => idp
      | true => idp

-- 7. Define factorial via Nat-rec (i.e., without recursion and pattern matching).

\func factorial (n : Nat) : Nat
  => Nat-rec Nat 1 (\lam x y => (suc x) * y) n

\func testFactorial0 : factorial 0 = 1 => idp
\func testFactorial1 : factorial 5 = 120 => idp

-- 8. Prove associativity of Nat.+ via Nat-elim (i.e., without recursion and pattern matching).

\open Nat(+)

\func +-assoc {x y z : Nat} : (x + y) + z = x + (y + z)
  => Nat-elim (\lam z => Path (\lam _ => Nat) ((x + y) + z) (x + (y + z)))
              idp (\lam _ => pmap suc) z

-- 9. Define recursor and eliminator for D.

\data D
  | con1 Nat
  | con2 D D
  | con3 (Nat -> D)

\func D-elim (P : D -> \Type)
             (c1 : \Pi (n : Nat) -> P (con1 n))
             (c2 : \Pi (d1 d2 : D) -> P d1 -> P d2 -> P (con2 d1 d2))
             (c3 : \Pi (f : Nat -> D) -> (\Pi (n : Nat) -> P (f n)) -> P (con3 f))
             (x : D) : P x \elim x
  | con1 n => c1 n
  | con2 d1 d2 => c2 d1 d2 (D-elim P c1 c2 c3 d1) (D-elim P c1 c2 c3 d2)
  | con3 f => c3 f (\lam n => D-elim P c1 c2 c3 (f n))

\func D-rec (P : \Type)
            (c1 : Nat -> P)
            (c2 : D -> D -> P -> P -> P)
            (c3 : (Nat -> D) -> (Nat -> P) -> P)
            (x : D) : P \elim x
  | con1 n => c1 n
  | con2 d1 d2 => c2 d1 d2 (D-rec P c1 c2 c3 d1) (D-rec P c1 c2 c3 d2)
  | con3 f => c3 f (\lam n => D-rec P c1 c2 c3 (f n))

-- 10. Define recursor and eliminator for List.

\data List (A : \Type) | nil | cons A (List A)

\func List-elim {A : \Type} (P : List A -> \Type)
                (n : P nil)
                (c : \Pi (a : A) (as : List A) -> P as -> P (cons a as))
                (x : List A) : P x \elim x
  | nil => n
  | cons a as => c a as (List-elim P n c as)

\func List-rec {A : \Type} (P : \Type)
                (n : P)
                (c : A -> List A -> P -> P)
                (x : List A) : P \elim x
  | nil => n
  | cons a as => c a as (List-rec P n c as)

-- 11. We defined transport via coe.
--     Define a special case of coe via transport.
--     Is it possible to define transport via coe0?

\func coe0 (A : I -> \Type) (a : A left) : A right
  => transport A left=right a

\func transport'' {A : \Type} (B : A -> \Type) {a a' : A} (p : a = a') (b : B a) : B a'
  => coe0 (\lam i => B (p @ i)) b

-- 12. Define a function B right -> B left.

\func right=left : right = left
  => inv left=right

\func Itr' {B : I -> \Type} (b : B right) : B left
  => transport B right=left b

-- 13. Prove that 0 does not equal to suc x.

\func T' (n : Nat) : \Type
  | 0 => Unit
  | _ => Empty

\func zero/=suc (x : Nat) (p : 0 = suc x) : Empty
  => transport T' p unit

-- 14. Prove that fac does not equal to suc.

\open Nat(*)

\func fac (n : Nat) : Nat
  | 0 => 1
  | suc n => suc n * fac n

\func T'' (n : Nat) : \Type
  | 1 => Unit
  | _ => Empty

\func fac/=suc (p : fac = suc) : Empty
  => fac/=suc' (path (\lam i => (p @ i) 1))
  \where
    \func fac/=suc' (p : fac 1 = suc 1) : Empty
      => transport T'' p unit
